apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.standardout.versioneye'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    defaultConfig {
        applicationId 'me.sweetll.tucao'
        minSdkVersion 18
        targetSdkVersion versions.targetSdk
        versionCode 117
        versionName '1.1.5'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters 'armeabi-v7a'/*, 'x86'*/
        }
    }
    signingConfigs {
        release {
            storeFile file("../keystore.jks")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dataBinding {
        enabled = true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    if (project.hasProperty("devBuild")) {
        splits.abi.enable = false
        splits.density.enable = false
        aaptOptions.cruncherEnabled = false
    }
}

versioneye {
    determineScopeStrategy = { Set<String> configs ->
        def scopes = ['compile']
        scopes
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
    testImplementation 'junit:junit:4.12'
    /* DialogPlus */
    implementation project(':dialogplus')
    /* File by file patch */
    implementation project(':applier')
    /* GSYVideoPlayer */
    implementation project(':gsyVideoPlayer')
    /* TakePhoto */
    implementation project(':takephoto_library')
    implementation 'com.android.support:multidex:1.0.2'
    /* Support Library */
    implementation "com.android.support:appcompat-v7:${versions.supportLibrary}"
    implementation "com.android.support:recyclerview-v7:${versions.supportLibrary}"
    implementation "com.android.support:cardview-v7:${versions.supportLibrary}"
    implementation "com.android.support:gridlayout-v7:${versions.supportLibrary}"
    implementation "com.android.support:design:${versions.supportLibrary}"
    /* Kotlin */
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
    kapt "com.android.databinding:compiler:${versions.gradle}"
    /* RxJava2 */
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    /* RxKotlin */
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    /* RxBinding */
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    /* RxLifecycle */
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-kotlin:2.2.0'
    /* RxPermissions */
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4'
    /* Retrofit */
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.moshi:moshi:1.5.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation('com.squareup.retrofit2:converter-simplexml:2.3.0') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    /* Dagger2 */
    implementation 'com.google.dagger:dagger:2.12'
    kapt 'com.google.dagger:dagger-compiler:2.12'
    /* Glide */
    implementation 'com.github.bumptech.glide:glide:4.3.0'
    implementation "com.android.support:support-v4:${versions.supportLibrary}"
    implementation 'com.github.bumptech.glide:annotations:4.3.0'
    kapt 'com.github.bumptech.glide:compiler:4.3.0'
    /* RecyclerView Adapter */
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.12'
    /* Jsoup */
    implementation 'org.jsoup:jsoup:1.10.3'
    /* Crash */
    implementation 'me.drakeet.library:crashwoodpecker:2.1.1'
    /* Umeng */
    implementation 'com.umeng.analytics:analytics:6.1.2'
    /* EventBus */
    implementation 'org.greenrobot:eventbus:3.0.0'
    /* Banner */
    implementation 'com.bigkoo:convenientbanner:2.0.5'
    /* About */
    implementation 'com.github.medyo:android-about-page:1.2'
    /* Leak Canary */
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.2'
//    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.2'
//    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.2'
    /* Block Canary */
    debugImplementation 'com.github.markzhai:blockcanary-android:1.5.0'
    releaseImplementation 'com.github.markzhai:blockcanary-no-op:1.5.0'
    /* DBFlow */
    kapt 'com.github.Raizlabs.DBFlow:dbflow-processor:4.0.5'
    implementation 'com.github.Raizlabs.DBFlow:dbflow-core:4.0.5'
    implementation 'com.github.Raizlabs.DBFlow:dbflow:4.0.5'
    implementation 'com.github.Raizlabs.DBFlow:dbflow-kotlinextensions:4.0.5'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
        transitive = true
    }
    /* BigImageViewer */
    implementation 'com.github.piasy:BigImageViewer:1.4.0'
    implementation 'com.github.piasy:GlideImageLoader:1.4.0'
    implementation 'com.github.piasy:ProgressPieIndicator:1.4.0'
}

repositories {
    mavenCentral()
}


apply plugin: 'kotlin-android-extensions'